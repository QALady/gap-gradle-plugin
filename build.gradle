repositories {
    maven {
        url "http://artifactory.gapinc.dev/artifactory/remote-repos"
    }
    ivy {
        layout "maven"
        url "http://artifactory.gapinc.dev/artifactory/remote-repos"
    }
}

ext.majorVersion = 2
ext.minorVersion = 0

group 'com.gap'
version = "${majorVersion}.${minorVersion}.${release}.${counter}"

apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'

apply plugin: 'sonar-runner'
apply from: 'gradle/sonarQube.gradle'

dependencies{
    groovy localGroovy()
    compile gradleApi()

    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.6'
    compile 'org.apache.httpcomponents:httpclient:4.3.1'

    /* == cobertura deps == */
    compile 'gradle_cobertura:gradle_cobertura:1.1'
    compile 'net.sourceforge.cobertura:cobertura:1.9.4.1'
    compile 'asm:asm-all:3.3.1'
    compile 'oro:oro:2.0.8'
    /* ==================== */

    testCompile ('junit:junit-dep:4.11'){
        exclude group:'org.hamcrest', module:'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile ('org.mockito:mockito-core:1.9.5'){
        exclude group:'org.hamcrest', module:'hamcrest-core'
    }
    testCompile 'org.objenesis:objenesis:2.0'
    testCompile 'com.cyrusinnovation:mockito-groovy-support:1.3'
}

sourceSets {
    integrationTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
        groovy {
            srcDir 'src/integrationTest/groovy'
        }
    }
}

task integrationTest(type: Test, dependsOn: 'test') {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn << 'integrationTest'

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                url: "http://artifactory.gapinc.dev/artifactory/local-non-prod",
                authentication: [userName: "admin", password: "password"]
            )
        }
        ivy {
            layout 'maven'
            url "http://artifactory.gapinc.dev/artifactory/local-non-prod"
            credentials {
                username "admin"
                password "password"
            }
        }
    }
}
