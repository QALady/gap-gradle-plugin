repositories {
    maven {
        url "http://artifactory.gapinc.dev/artifactory/remote-repos"
    }
    ivy {
        layout "maven"
        url "http://artifactory.gapinc.dev/artifactory/remote-repos"
    }
}

buildscript{
    repositories{
        maven {
            url "http://artifactory.gapinc.dev/artifactory/remote-repos"
        }
        ivy {
            layout "maven"
            url "http://artifactory.gapinc.dev/artifactory/remote-repos"
        }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:1.2.1"
    }
}

ext.majorVersion = 2
ext.minorVersion = 0

group 'com.gap'
version = "${majorVersion}.${minorVersion}.${release}.${counter}"

apply plugin: 'maven'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'cobertura'

if (gradle.gradleVersion.equals('1.11')) {
    apply plugin: 'sonar-runner'
    apply from: 'gradle/sonarQube.gradle'
}

cobertura {
    coverageFormats = ['xml', 'html']
    coverageReportDir = mkdir('target/reports/coverage')
}

dependencies{

    if (gradle.gradleVersion.equals('1.11')) {
        compile "org.codehaus.groovy:groovy-all:2.2.0"
    }
    else {
        groovy "org.codehaus.groovy:groovy-all:2.2.0"
    }
    compile gradleApi()

    compile('org.codehaus.groovy.modules.http-builder:http-builder:0.6') {
		exclude(module: 'groovy')
	}
    compile 'org.apache.httpcomponents:httpclient:4.3.1'

    /* == cobertura deps == */
//    compile ('gradle_cobertura:gradle_cobertura:1.1') {
//		exclude(module: 'asm')
//    }
    compile ('net.sourceforge.cobertura:cobertura:2.0.3')
//    compile 'oro:oro:2.0.8'
    /* ==================== */

    testCompile ('junit:junit-dep:4.11'){
        exclude group:'org.hamcrest', module:'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile ('org.mockito:mockito-core:1.9.5'){
        exclude group:'org.hamcrest', module:'hamcrest-core'
    }
    testCompile 'org.objenesis:objenesis:2.0'
    testCompile 'com.cyrusinnovation:mockito-groovy-support:1.3'
}

sourceSets {
    integrationTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
        groovy {
            srcDir 'src/integrationTest/groovy'
        }
    }
    functionalTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
        groovy {
            srcDir 'functionalTests/groovy'
        }
    }
}

task integrationTest(type: Test, dependsOn: 'test') {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functionalTest(type: Test) {
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(
                url: "http://artifactory.gapinc.dev/artifactory/local-non-prod",
                authentication: [userName: "admin", password: "password"]
            )
        }
        ivy {
            layout 'maven'
            url "http://artifactory.gapinc.dev/artifactory/local-non-prod"
            credentials {
                username "admin"
                password "password"
            }
        }
    }
}
